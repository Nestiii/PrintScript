plugins {
    id 'java'
    id "com.github.sherter.google-java-format" version "0.9"
    id 'jacoco'
    id 'com.novoda.static-analysis' version '1.2'
    id 'maven-publish'
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile 'info.picocli:picocli:4.3.2'
    annotationProcessor 'info.picocli:picocli-codegen:4.3.2'
    compile project(":lexer")
    compile project(":parser")
    compile project(":interpreter")
}

test {
    finalizedBy jacocoTestReport // report is always generated after tests run
}
jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

jacoco {
    toolVersion = "0.8.6"
}

jar {
    manifest {
        attributes(
                'Main-Class': 'Main'
        )
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
group 'com.austral'
version '1.0.1'

tasks.build.dependsOn(tasks.googleJavaFormat)
verifyGoogleJavaFormat.mustRunAfter(tasks.googleJavaFormat)

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Nestiii/PrintScript")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
